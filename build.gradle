apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

group               = 'com.bloidonia'
version             = '1.0-SNAPSHOT'
sourceCompatibility = 1.6
targetCompatibility = 1.6

compileJava.options.compilerArgs << "-Xlint:unchecked"

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'maven central' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
}


// In this section you declare the dependencies for your production and test code
dependencies {
    compile     'org.codehaus.groovy:groovy-all:2.2.1'
    compile     'org.apache.commons:commons-lang3:3.1'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testRuntime fileTree( dir:'build/libs/', includes:[ "groovy-tpyo-${version}.jar".toString() ] )
}

def metaServices = new File( 'src/main/groovy/META-INF/services' )

task generateModule << {
    metaServices.mkdirs()
    new File( metaServices, 'org.codehaus.groovy.runtime.ExtensionModule' ).withWriter { w ->
        w.writeLine 'moduleName=groovy-tpyo'
        w.writeLine "moduleVersion=$version"
        w.writeLine 'extensionClasses=com.bloidonia.tpyo.TpyoExtension'
        w.writeLine 'staticExtensionClasses='
    }
}

compileGroovy.dependsOn( generateModule )
test.dependsOn( jar )
uploadArchives.dependsOn( test )

jar {
    from( metaServices ) {
        include 'org.codehaus.groovy.runtime.ExtensionModule' into( 'META-INF/services' )
    }
}

task groovydocJar( type: Jar, dependsOn: groovydoc ) {
    classifier 'javadoc'
    from groovydoc.destinationDir
}

task sourcesJar( type: Jar ) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
    archives groovydocJar
}

if( !hasProperty( 'sonatypeUsername' ) ) {
    ext.sonatypeUsername = ''
}

if( !hasProperty( 'sonatypePassword' ) ) {
    ext.sonatypePassword = ''
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
 
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Tpyo'
                description 'Auto correct typos in your code'
                url 'https://github.com/timyates/groovy-tpyo'
                inceptionYear '2013'

                scm {
                    url 'https://github.com/timyates/groovy-tpyo'
                    connection 'scm:http://github.com/timyates/groovy-tpyo'
                    developerConnection 'scm:git://github.com/timyates/groovy-tpyo.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'tim_yates'
                        name 'Tim Yates'
                        email 'tim.yates@gmail.com'
                    }
                }
            }
        }
    }
}
